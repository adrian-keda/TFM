configfile: 'config.yaml'
chr = config['chr']
chunks = ["0", "1", "2", "3", "4", "5", "6", "7", "8", "9"]

rule all:
    input:
        expand('out/s9_joined/{chr}.long.carriers.tsv', chr = chr),
        expand('out/s10_vaf/{chr}.long.carriers.vaf.tsv.gz', chr = chr)


rule s1_get_header_line:
    input:
        comp = '/storage/scratch01/users/jaejlee/011.Exonic_ID_and_VCF_filter/003.VCF_FILTER_AGAIN/out/s2_filter_vcf/TCGA.FIL.{chr}.count.vcf.gz'
    output:
        header = temp('out/s1_header/{chr}.header.tsv')
    resources:
        mem_mb = 10240,
        walltime = 30
    shell:
        'bcftools head {input.comp} | tail -n 1 > {output.header}'


rule s2_remove_header:
    input:
        comp = '/storage/scratch01/users/jaejlee/011.Exonic_ID_and_VCF_filter/003.VCF_FILTER_AGAIN/out/s2_filter_vcf/TCGA.FIL.{chr}.count.vcf.gz'
    output:
        comp_nohead = temp('out/s2_no_header/{chr}.no_header.tsv.gz')
    resources:
        mem_mb = 51200,
        walltime = 120
    shell:
        'bcftools view -H -O z -o {output.comp_nohead} {input.comp}'


rule s3_decompress:
    input:
        comp = 'out/s2_no_header/{chr}.no_header.tsv.gz'
    output:
        decomp = temp('out/s3_decompressed/{chr}.no_header.decompressed.tsv')
    resources:
        mem_mb = 51200,
        walltime = 240
    shell:
        'gunzip -d -k -c {input.comp} > {output.decomp}'


rule s4_split:
    input:
        decomp = 'out/s3_decompressed/{chr}.no_header.decompressed.tsv'
    output:
        chunks = temp(expand('out/s4_split/{{chr}}/{{chr}}.no_header.decompressed.split.tsv_{chunk}', chunk = chunks))
    resources:
        mem_mb = 51200,
        walltime = 120
    params:
        prefix = "out/s4_split/{chr}/{chr}.no_header.decompressed.split.tsv_"
    priority: 2
    shell:
        'split -n l/10 -d -a 1 {input.decomp} {params.prefix}'


rule s5_paste_header:
    input:
        chunk = 'out/s4_split/{chr}/{chr}.no_header.decompressed.split.tsv_{chunk}',
        header = 'out/s1_header/{chr}.header.tsv'
    output:
        chunk_header = temp('out/s5_paste_header/{chr}/{chr}.header.decompressed.split.tsv_{chunk}')
    resources:
        mem_mb = 51200,
        walltime = 120
    priority: 3
    shell:
        'cat {input.header} {input.chunk} > {output.chunk_header}'


rule s6_wide_to_long:
    input:
        chunk_header = 'out/s5_paste_header/{chr}/{chr}.header.decompressed.split.tsv_{chunk}'
    output:
        chunk_table = temp('out/s6_long_chunks/{chr}/{chr}.long.{chunk}.tsv')
    resources:
        mem_mb = 100000,
        walltime = 1440
    params:
        preheader = config['preheader'],
        infos = config['infos'],
        formats = config['formats']
    priority: 4
    shell:
        'python3 /home/amaqueda/VCF-Simplify/VcfSimplify.py SimplifyVCF -toType table -inVCF {input.chunk_header} -outFile {output.chunk_table} -preHeader {params.preheader} -infos {params.infos} -formats {params.formats} -mode long'


rule s7_get_keep_carriers:
    input:
        chunk_table = 'out/s6_long_chunks/{chr}/{chr}.long.{chunk}.tsv'
    output:
        carriers_table = temp('out/s7_carrires_chunks/{chr}/{chr}.long.carriers.{chunk}.tsv')
    resources:
        mem_mb = 51200,
        walltime = 240
    priority: 5
    shell:
        'egrep -v "(\./\.)|(0/0)" {input.chunk_table} > {output.carriers_table}'


rule s8_rem_header:
    input:
        carriers_table = 'out/s7_carrires_chunks/{chr}/{chr}.long.carriers.{chunk}.tsv'
    output:
        carriers_table_nohead = temp('out/s8_carrires_chunks_nohead/{chr}/{chr}.long.carriers.nohead.{chunk}.tsv')
    resources:
        mem_mb = 51200,
        walltime = 120
    priority: 6
    shell:
        'tail -n +2 {input.carriers_table} > {output.carriers_table_nohead}'


rule s9_join_output:
    input:
        long_header = 'header_long.tsv',
        carriers_chunks_nohead = expand('out/s8_carrires_chunks_nohead/{{chr}}/{{chr}}.long.carriers.nohead.{chunk}.tsv', chunk = chunks)
    output:
        out = 'out/s9_joined/{chr}.long.carriers.tsv'
    resources:
        mem_mb = 75000,
        walltime = 120
    priority: 7
    shell:
        'cat {input.long_header} {input.carriers_chunks_nohead} > {output.out}'


rule s10_compute_vaf:
    input:
        tab = 'out/s9_joined/{chr}.long.carriers.tsv'
    output:
        vaf = 'out/s10_vaf/{chr}.long.carriers.vaf.tsv.gz'
    resources:
        mem_mb = 75000,
        walltime = 360
    script:
        'vaf_calculator.py'
    

# snakemake --use-conda --profile $SMK_PROFILE_SLURM -j 25 -n